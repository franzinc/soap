;; -*- mode: common-lisp; package: net.xmp -*-
;;
;; copyright (c) 2003 Franz Inc, Berkeley, CA - All rights reserved.
;; copyright (c) 2003-2012 Franz Inc, Oakland, CA - All rights reserved.
;;
;; The software, data and information contained herein are proprietary
;; to, and comprise valuable trade secrets of, Franz, Inc.  They are
;; given in confidence by Franz, Inc. pursuant to a written license
;; agreement, and may be stored and used only in accordance with the terms
;; of such license.
;;
;; Restricted Rights Legend
;; ------------------------
;; Use, duplication, and disclosure of the software, data and information
;; contained herein by any agency, department or entity of the U.S.
;; Government are subject to restrictions of Restricted Rights for
;; Commercial Software developed at private expense as specified in
;; DOD FAR Supplement 52.227-7013 (c) (1) (ii), as applicable.

;; $Id: xmp-aserve.cl,v 2.7 2007/04/17 21:50:41 layer Exp $

;; Using AllegroServe as the transport layer.

(in-package :net.xmp)

(eval-when (compile load eval)
  (require :aserve)
  )

(defpackage :net.xmp
  (:use :net.aserve :net.aserve.client :net.html.generator)
  (:export

   ;; Classes
   xmp-aserve-connector

   xmp-aserve-client-connector
   xmp-aserve-server-connector

   xmp-aserve-string-out-connector
   xmp-aserve-stream-out-connector

   xmp-aserve-string-in-connector
   xmp-aserve-event-in-connector

   xmp-aserve-string-in-out-connector
   xmp-aserve-event-string-connector
   xmp-aserve-string-stream-connector
   xmp-aserve-event-stream-connector
   
   xmp-aserve-client-string-out-connector
   xmp-aserve-client-stream-out-connector
   xmp-aserve-client-string-in-connector
   xmp-aserve-client-event-in-connector
   xmp-aserve-client-string-in-out-connector
   xmp-aserve-client-event-string-connector
   xmp-aserve-client-string-stream-connector
   xmp-aserve-client-event-stream-connector

   xmp-aserve-server-string-out-connector
   xmp-aserve-server-stream-out-connector
   xmp-aserve-server-string-in-connector
   xmp-aserve-server-event-in-connector
   xmp-aserve-server-string-in-out-connector
   xmp-aserve-server-event-string-connector
   xmp-aserve-server-string-stream-connector
   xmp-aserve-server-event-stream-connector

   ;; Slot names 
   host agent content-type http-protocol method url

   ;; Accessors
   xmp-destination-url
   xmp-more-headers

   ;; Generic functions
   xmp-server-response 
   
   ;; Ordinary functions
   xmp-header-slot
   
   ))


(def-xmp-sub-classes ("xmp-aserve" "connector") (("xmp" "connector"))
  (nil
   ((transport :initform :aserve)
    (host          :accessor xmp-destination-host
		   :initarg :http-host :initform nil)
    (agent         :accessor xmp-destination-agent
		   :initarg :http-agent :initform nil)
    (content-type  :accessor xmp-destination-content-type 
		   :initarg :content-type :initform nil)
    (http-protocol :accessor xmp-destination-http-protocol
		   :initarg :http-protocol :initform nil)
    (method        :accessor xmp-destination-method
		   :initarg :http-method :initform nil)
    (more-headers  :accessor xmp-more-headers :initarg :http-headers :initform nil)
    (url           :accessor xmp-destination-url    :initform nil :initarg :url)
    (name          :accessor xmp-server-name
		   :initform (format 
			      nil 
			      "AllegroServe/廉廉窿领戾珧蔑眄镱涕箴狍弪鲥鲥蝮轱瞠轰镢蹴孱翎糸镱㈩锃眇泔瘗疳蜥礤翦蝮横沣弩箫眇箦蝣弪疳蜥礤翦蝮洪铋翎蜱吼狎犴弭弪洪铋翩矧铋飑蝈聃弩横沣弩箫狍弪鲥蝈聃弩洪铋翩矧铋轰镢蹴孱翎糸镱㈩锃眇泔瘗┅á箦蝣弪è箦蝣弪横沣弩箫眇狍弪鲥箦蝣弪洪铋翩矧铋飑┅＋殓铒蝈ㄤ彐礤翳镤眇泔瘗横蝻躅è镡赍泗眇狍弪鲥泔铑邈麸颟脲犰祜鳝雉桢颦脲螬戾è铄ㄣ犰飙铄舡礤翳镤┅箦翩眇溴篝轭狒轱瞽栾篝铄鳗眇溴篝轭狒轱瞽栾篝镡赍泗眇溴篝轭狒轱瞽徵孱铄鳗眇溴篝轭狒轱瞽徵孱镡赍泗眇溴篝轭狒轱瞽泔铘孱舡豉疱铄鳗眇溴篝轭狒轱瞽泔铘孱舡豉疱镡赍泗眇溴篝轭狒轱瞽梏麴痱雉镢镬铄鳗眇溴篝轭狒轱瞽梏麴痱雉镢镬镡赍泗眇溴篝轭狒轱瞽礤翳镤铄鳗眇溴篝轭狒轱瞽礤翳镤镡赍泗眇溴篝轭狒轱瞽躜铄鳗眇溴篝轭狒轱瞽躜镡赍泗眇箦蝣弪疳蜥礤翦蝮铄鳗眇箦蝣弪疳蜥礤翦蝮镡赍泗铄鳗＋殓铒蝈ㄤ彐礤翳镤眇泔瘗横蝻躅è镡赍泗眇狍弪鲥箦蝣弪泔铑邈麸颟脲犰祜鳝雉桢颦脲螬戾è铄ㄣ犰飙铄舡礤翳镤┅箦翩眇狍弪鲥箦蝣弪铄鳗眇狍弪鲥箦蝣弪镡赍泗铄鳗ㄤ彐礤翳镤眇屮翦蝾犰骘蝽狒è泔铑眇狍弪鲥泔铑邈麸颟矧豉疱汜箦眇盱孱泔溟铉泔铑è礤礅弪铋飑铋飑ㄣ镱ㄦ轵篝眇盱孱泔溟铉泔铑┅雉桢蝼轶眇盱孱泔溟铉泔铑┅溴驷蹯舡狍弪鲥屮翦蝾犰骘蝽狒┅ㄤ彐礤翳镤眇礤篌徵瀛箦钿è泔铑眇狍弪鲥沆殄铘篝蜷铉轭秕舡泔铑邈麸颟脲桢徜弪犰祜鳝雉桢颦脲螬眭祠轲戾鲠祯瀛忾钿蜚ㄡ痧禊т锃梏麴蝈聃弩眇溴篝轭狒轱瞽躜泔铑喉弭栾眇溴篝轭狒轱瞽礤翳镤泔铑吼蝻麸泔眇溴篝轭狒轱瞽梏麴痱雉镢镬泔铑恒镱翦铘豉疱眇溴篝轭狒轱瞽泔铘孱舡豉疱泔铑恒镱翦铘眇礤篌徵瀛篝蜷铉泔铑乎箦颦徵孱眇溴篝轭狒轱瞽徵孱泔铑鸿遽溴蝮ㄡ痧孱戾è栾篝眇溴篝轭狒轱瞽栾篝泔铑┅麒孱栾篝啜á蕊篝栾篝┅┅桢徜弪眇盹蝈桢徜弪泔铑换垓驽恫吵哄翦蝾犰骘蝽狒眇屮翦蝾犰骘蝽狒泔铑眇沆殄铘篝狎泔铑┅ㄣ狍蜚ú鞍螬ǖ鞍螬雉桢蝼轶眇弪蝻泔铑后弪鲥后踱泔溴鸿趑瓠蝈箴镱箦泔溴后趄轭蜚轰弭衢螬┅ㄤ彐礤翳镤眇礤篌徵瀛箦钿è泔铑眇狍弪鲥箦蝣弪篝蜷铉轭秕舡泔铑邈麸颟脲蝈聃弩孱糸豉犰祜鳝雉桢颦脲螬鏖翳梏麴蝈箴镱箦蝈聃弩孱糸豉恒镱翦铘豉疱矧眇溴篝轭狒轱瞽泔铘孱舡豉疱泔铑Ⅳ屮舣盱鏖翳梏麴怙澌蝈聃弩孱糸豉哄翦蝾犰骘蝽狒眇屮翦蝾犰骘蝽狒泔铑鸿遽溴蝮啜ê箦蝣弪眇箦蝣弪钺礤泔铑┅括眇盹蝈桢徜弪泔铑换垓驽恫吵┅ㄨ繇ê痱轭眇礤篌徵瀛篝蜷铉泔铑┅┅┅ㄤ彐礤翳镤眇篝狎舡箦蝣弪è泔铑眇狍弪鲥箦蝣弪泔铑邈麸颟脲铄篝狎铋蟓皓孱徕戾犰祜鳝雉桢颦脲螬麒孱铄箦翩眇狍弪鲥箦蝣弪泔铑磲脲轭篝犷沐ь弭狍弪鲥瑚箦蝣弪┅箦翩眇狍弪鲥箦蝣弪泔铑ㄡ痧禊篝狎ㄡ痧孱麒孱眇狍弪鲥箦蝣弪泔铑扉篝后弪鲥眇狍弪鲥箦蝣弪泔铑┅ㄩ蟓篝狎眇箦蝣弪篝狎泔铑┅┅麒孱孱徕戾眇孱徕戾箦蝣弪泔铑┅泔铑ㄤ彐礤翳镤眇篝镳箦蝣弪è泔铑眇狍弪鲥箦蝣弪泔铑邈麸颟脲溟筢忪犰祜鳝雉桢颦脲螬麒孱溟筢忪眇溟筢忪瀛箦蝣弪泔铑┅麒孱眇狍弪鲥箦蝣弪泔铑箬豸滹黝后弪鲥眇狍弪鲥箦蝣弪泔铑┅泔铑ㄤ彐礤翳镤眇孱徕戾箦蝣弪横蝻躅è箦蝣弪眇狍弪鲥箦蝣弪泔铑邈麸颟脲犰祜鳝雉桢颦脲螬矧眇狍弪鲥箦蝣弪箦蝣弪箦翩眇狍弪鲥箦蝣弪箦蝣弪铄舢狍弪鲥邯黧弪鲥颡┅ㄣ犰飙铄舡礤翳镤ㄡ痧禊＇瘐忪轶后弪鲥眇狍弪鲥箦蝣弪箦蝣弪烘躅泗轱＇灬礅溽蝈聃弩孱糸豉眇箦蝣弪蝈箴镱箦箦蝣弪候羼蹂篝蝈聃弩哄铘轸孱糸豉┅恒镱翦铘豉疱Ⅳ屮舣盱眇箦蝣弪疳蜥礤翦蝮箦蝣弪┅箦蝣弪ㄤ彐礤翳镤眇箦蝣弪蝈箴镱箦è箦蝣弪眇狍弪鲥箦蝣弪泔铑邈麸颟脲蝈聃弩孱糸豉镳糸镱犰祜鳝雉桢颦脲螬戾è箦蝣弪眇瑚轸璀痱镢弩蟓祜汶è眇箦蝣弪祜汶箦蝣弪┅换歪脲泔瘗镦翳箦蝣弪麸犰祜眭祠轲戾换狍弪鲥黠螂弪翳蝈徜眇泔瘗箦蝣弪┅┅箦翩ㄡ箦蝣瀛蝈聃弩箦蝣弪蝈聃弩舂ㄡ痧禊眇箦蝣弪轫痨屙孱翎糸镱箦蝣弪ㄧ弭蝈聃弩舡怙澌蝈聃弩舂镳糸镱螬眇礤篌徵瀛箦钿箦蝣弪候羼蹂篝蝈聃弩哄铘轸孱糸豉┅ㄤ彐躅眇桢徜弪箪雉蝈聃弩箪雉换狍弪鲥铒蝽犰辁弩桢徜弪钺礤翳轶麽箦翩箪雉蝈徜骝镯篝蜷铉ㄣ镱汜翦钺翦篝蜷铉⒑篝蜷铉滹黝汜箦簌礅镬钺礤箪雉┅┅换蝈盹鲥篝蜷铉聃雉弩垠痱补泊递戾舄è鲠ㄨ遽溴颦箪雉鲠祯蝈聃弩箪雉┅戾戾铉翳鲠飑┅ㄣ镱è戾博鲠飑è犷ㄥ耢＼ㄥ祠鲠癌ㄥ耢＼ㄥ祠鲠ū戾瞟┅篚怏羼鲠ū戾瞟┅è犷ㄥ耢＼ㄥ祠鲠癌ㄥ耢＼ㄥ祠鲠ū戾瞟┅篚怏羼鲠ū戾瞟┅鲠飑┅